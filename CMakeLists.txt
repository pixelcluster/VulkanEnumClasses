cmake_minimum_required(VERSION 3.12)

cmake_policy(SET CMP0076 NEW)

project(VulkanEnumClasses VERSION 0.9 LANGUAGES CXX)

set(XMLPATH "" CACHE STRING "An optional override path to vk.xml. If empty, the vk.xml of the Vulkan SDK will be used.")
set(NAMESPACE "" CACHE STRING "An optional namespace in which all enums will be defined. Empty means no namespace. There must not be any whitespace in the string.")

set(AUTO_INCLUDE_EXTENSIONS TRUE CACHE BOOL "When enabled, all enums defined in vk.xml except explicitly excluded ones will be generated. When disabled, only core enums and explicitly enabled extensions will be generated.")
set(EXTENSION_EXCLUDES "" CACHE STRING "Names of extensions whose enums to ignore, spearated by \';\'. Has no effect if AUTO_INCLUDE_EXTENSIONS is FALSE. There must not be any whitespace in the string. Disabling an extension will automatically disable all extensions that depend on it.")
set(EXTENSION_INCLUDES "" CACHE STRING "Names of extensions whose enums to include, spearated by \';\'. Has no effect if AUTO_INCLUDE_EXTENSIONS is TRUE. There must not be any whitespace in the string. Enabling an extension will automatically enable all extensions it depends on.")

set(REPLACE_NAMES TRUE CACHE BOOL "Toggles whether the enum names will be changed. If this value is false, all options modifying enum names have no effect.")
set(NAME_PREFIX_REPLACEMENT "" CACHE STRING "A string that prefixes all enum names instead of \"Vk\". There must not be any whitespace in the string.")
set(NAME_REMOVE_POSTFIX FALSE CACHE BOOL "Toggles whether the postfixes on extension enum names (e.g. EXT, KHR) will be kept or removed.")

set(REPLACE_VALUES TRUE CACHE BOOL "Toggles whether to replae enum names or keep them as in vk.xml.")
set(VALUE_PREFIX_REPLACEMENT "" CACHE STRING "The prefix applied to all values instead of \"VK_\". There must not be any whitespace in the string.")
set(VALUE_NUMBER_PREFIX "_" CACHE STRING "The prefix applied to all values that have a leading digit (which is illegal). There must not be any whitespace in the string.")
set(VALUE_REMOVE_UNDERSCORES TRUE CACHE BOOL "Toggles whether to remove underscores from structure names.")
set(VALUE_REMOVE_STRUCTURE_NAMES TRUE CACHE BOOL "If FALSE, the name of the enum will be removed from all values of the enum.")
set(VALUE_TOLOWER TRUE CACHE BOOL "If TRUE, enum values will be converted to lowercase.")
set(VALUE_CAPITALIZE_START TRUE CACHE BOOL "If TRUE, the beginning of each word in the value is capitalized (ENUM_VALUE becomes EnumValue)")
set(VALUE_REMOVE_POSTFIX TRUE CACHE BOOL "Toggles whether the postfixes on extension enum values (e.g. EXT, KHR) will be kept or removed.")
set(VALUE_REMOVE_POSTFIX_NONPOSTFIX_TYPE FALSE CACHE BOOL "Toggles whether the postfixes on extension enum values (e.g. EXT, KHR) of structures that do not have a postfix will be kept or removed.")

add_subdirectory(dependencies/tinyxml EXCLUDE_FROM_ALL)
add_executable(generate generator/main.cpp generator/generate.hpp generator/generate.cpp generator/parsing_utils.hpp)
target_link_libraries(generate tinyxml2)
set_target_properties(generate PROPERTIES OUTPUT_NAME generateHeader)

if(XMLPATH STREQUAL "")
	list(APPEND GENERATOR_ARGLIST "--path" "$ENV{VULKAN_SDK}/share/vulkan/registry/vk.xml")
else()
	list(APPEND GENERATOR_ARGLIST "--path" "${XMLPATH}")
endif()

if(NOT NAMESPACE STREQUAL "")
	list(APPEND GENERATOR_ARGLIST "--namespace" ${NAMESPACE})
endif()
if(AUTO_INCLUDE_EXTENSIONS EQUAL FALSE)
	list(APPEND GENERATOR_ARGLIST "--exclude-extensions")
	if(NOT EXTENSION_INCLUDES STREQUAL "")
		list(APPEND GENERATOR_ARGLIST "--include" ${EXTENSION_INCLUDES})
	endif()
elseif(NOT EXTENSION_EXCLUDES STREQUAL "")
		list(APPEND GENERATOR_ARGLIST "--exclude" ${EXTENSION_EXCLUDES})
endif()
if(${REPLACE_NAMES})
	list(APPEND GENERATOR_ARGLIST "--replace-names")
	list(APPEND GENERATOR_ARGLIST "--name-prefix-replacement" ${NAME_PREFIX_REPLACEMENT})
	if(${NAME_REMOVE_POSTFIX})
		list(APPEND GENERATOR_ARGLIST "--name-remove-postfix")
	endif()
endif()
if(${REPLACE_VALUES})
	list(APPEND GENERATOR_ARGLIST "--replace-values")
	if("${VALUE_PREFIX_REPLACEMENT}" STREQUAL "" AND "${VALUE_NUMBER_PREFIX}" STREQUAL "")
		message(WARNING "Neither the replacement prefix nor the number prefix are set! This might result in non-compilable code.")
	endif()
	list(APPEND GENERATOR_ARGLIST "--value-prefix-replacement" ${VALUE_PREFIX_REPLACEMENT})
	list(APPEND GENERATOR_ARGLIST "--value-number-prefix" ${VALUE_NUMBER_PREFIX})
	if(${VALUE_REMOVE_UNDERSCORES})
		list(APPEND GENERATOR_ARGLIST "--remove-underscores")
	endif()
	if(${VALUE_REMOVE_STRUCTURE_NAMES})
		list(APPEND GENERATOR_ARGLIST "--remove-structure-names")
	endif()
	if(${VALUE_TOLOWER})
		list(APPEND GENERATOR_ARGLIST "--tolower")
	endif()
	if(${VALUE_CAPITALIZE_START})
		list(APPEND GENERATOR_ARGLIST "--capitalize-start")
	endif()
	if(${VALUE_REMOVE_POSTFIX})
		list(APPEND GENERATOR_ARGLIST "--value-remove-postfix")
	endif()
	if(${VALUE_REMOVE_POSTFIX_NONPOSTFIX_TYPE})
		list(APPEND GENERATOR_ARGLIST "--value-remove-postfix-core-types")
	endif()
endif()

string(REPLACE ";" " " GENERATOR_ARGS "${GENERATOR_ARGLIST}")

message("Current arguments: ${GENERATOR_ARGS}")
add_custom_command(OUTPUT "include/VulkanEnums.hpp" COMMAND generate
ARGS ${GENERATOR_ARGLIST} WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/include" COMMENT "Executing command with args: ${GENERATOR_ARGS}"
DEPENDS generate "$ENV{VULKAN_SDK}/share/vulkan/registry/vk.xml" VERBATIM USES_TERMINAL)

add_library(VulkanEnumClasses INTERFACE "include/VulkanEnums.hpp")
target_include_directories(VulkanEnumClasses INTERFACE "include")
set_target_properties(VulkanEnumClasses PROPERTIES PUBLIC_HEADER "include/VulkanEnums.hpp")

include(GNUInstallDirs)
install(TARGETS VulkanEnumClasses PUBLIC_HEADER DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}")