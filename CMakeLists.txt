cmake_minimum_required(VERSION 3.15)

cmake_policy(SET CMP0076 NEW)

project(VulkanEnumClasses VERSION 0.9 LANGUAGES CXX)

set(VKENUMCLASSES_XMLPATH "" CACHE STRING "An optional override path to vk.xml. If empty, the vk.xml of the Vulkan SDK will be used.")
set(VKENUMCLASSES_NAMESPACE "" CACHE STRING "An optional namespace in which all enums will be defined. Empty means no namespace. There must not be any whitespace in the string.")

set(VKENUMCLASSES_AUTO_INCLUDE_EXTENSIONS TRUE CACHE BOOL "When enabled, all enums defined in vk.xml except explicitly excluded ones will be generated. When disabled, only core enums and explicitly enabled extensions will be generated.")
set(VKENUMCLASSES_EXTENSION_EXCLUDES "" CACHE STRING "Names of extensions whose enums to ignore, spearated by \';\'. Has no effect if VKENUMCLASSES_AUTO_INCLUDE_EXTENSIONS is FALSE. There must not be any whitespace in the string. Disabling an extension will automatically disable all extensions that depend on it.")
set(VKENUMCLASSES_EXTENSION_INCLUDES "" CACHE STRING "Names of extensions whose enums to include, spearated by \';\'. Has no effect if VKENUMCLASSES_AUTO_INCLUDE_EXTENSIONS is TRUE. There must not be any whitespace in the string. Enabling an extension will automatically enable all extensions it depends on.")

set(VKENUMCLASSES_REPLACE_NAMES TRUE CACHE BOOL "Toggles whether the enum names will be changed. If this value is false, all options modifying enum names have no effect.")
set(VKENUMCLASSES_NAME_PREFIX_REPLACEMENT "" CACHE STRING "A string that prefixes all enum names instead of \"Vk\". There must not be any whitespace in the string.")
set(VKENUMCLASSES_NAME_REMOVE_POSTFIX FALSE CACHE BOOL "Toggles whether the postfixes on extension enum names (e.g. EXT, KHR) will be kept or removed.")

set(VKENUMCLASSES_REPLACE_VALUES TRUE CACHE BOOL "Toggles whether to replae enum names or keep them as in vk.xml.")
set(VKENUMCLASSES_VALUE_PREFIX_REPLACEMENT "" CACHE STRING "The prefix applied to all values instead of \"VK_\". There must not be any whitespace in the string.")
set(VKENUMCLASSES_VALUE_NUMBER_PREFIX "_" CACHE STRING "The prefix applied to all values that have a leading digit (which is illegal). There must not be any whitespace in the string.")
set(VKENUMCLASSES_VALUE_REMOVE_UNDERSCORES TRUE CACHE BOOL "Toggles whether to remove underscores from structure names.")
set(VKENUMCLASSES_VALUE_REMOVE_STRUCTURE_NAMES TRUE CACHE BOOL "If FALSE, the name of the enum will be removed from all values of the enum.")
set(VKENUMCLASSES_VALUE_TOLOWER TRUE CACHE BOOL "If TRUE, enum values will be converted to lowercase.")
set(VKENUMCLASSES_VALUE_CAPITALIZE_START TRUE CACHE BOOL "If TRUE, the beginning of each word in the value is capitalized (ENUM_VALUE becomes EnumValue)")
set(VKENUMCLASSES_VALUE_REMOVE_POSTFIX TRUE CACHE BOOL "Toggles whether the postfixes on extension enum values (e.g. EXT, KHR) will be kept or removed.")
set(VKENUMCLASSES_VALUE_REMOVE_POSTFIX_NONPOSTFIX_TYPE FALSE CACHE BOOL "Toggles whether the postfixes on extension enum values (e.g. EXT, KHR) of structures that do not have a postfix will be kept or removed.")

add_subdirectory(dependencies/tinyxml EXCLUDE_FROM_ALL)
add_executable(generate generator/main.cpp generator/generate.hpp generator/generate.cpp generator/parsing_utils.hpp)
target_link_libraries(generate tinyxml2)
set_target_properties(generate PROPERTIES OUTPUT_NAME generateHeader)

if(VKENUMCLASSES_XMLPATH STREQUAL "")
	list(APPEND GENERATOR_ARGLIST "--path" "$ENV{VULKAN_SDK}/share/vulkan/registry/vk.xml")
else()
	list(APPEND GENERATOR_ARGLIST "--path" "${VKENUMCLASSES_XMLPATH}")
endif()

if(NOT VKENUMCLASSES_NAMESPACE STREQUAL "")
	list(APPEND GENERATOR_ARGLIST "--namespace" ${VKENUMCLASSES_NAMESPACE})
endif()
if(VKENUMCLASSES_AUTO_INCLUDE_EXTENSIONS EQUAL FALSE)
	list(APPEND GENERATOR_ARGLIST "--exclude-extensions")
	if(NOT VKENUMCLASSES_EXTENSION_INCLUDES STREQUAL "")
		list(APPEND GENERATOR_ARGLIST "--include" ${VKENUMCLASSES_EXTENSION_INCLUDES})
	endif()
elseif(NOT VKENUMCLASSES_EXTENSION_EXCLUDES STREQUAL "")
		list(APPEND GENERATOR_ARGLIST "--exclude" ${VKENUMCLASSES_EXTENSION_EXCLUDES})
endif()
if(${VKENUMCLASSES_REPLACE_NAMES})
	list(APPEND GENERATOR_ARGLIST "--replace-names")
	list(APPEND GENERATOR_ARGLIST "--name-prefix-replacement" ${VKENUMCLASSES_NAME_PREFIX_REPLACEMENT})
	if(${VKENUMCLASSES_NAME_REMOVE_POSTFIX})
		list(APPEND GENERATOR_ARGLIST "--name-remove-postfix")
	endif()
endif()
if(${VKENUMCLASSES_REPLACE_VALUES})
	list(APPEND GENERATOR_ARGLIST "--replace-values")
	if("${VKENUMCLASSES_VALUE_PREFIX_REPLACEMENT}" STREQUAL "" AND "${VKENUMCLASSES_VALUE_NUMBER_PREFIX}" STREQUAL "")
		message(WARNING "Neither the replacement prefix nor the number prefix are set! This might result in non-compilable code.")
	endif()
	list(APPEND GENERATOR_ARGLIST "--value-prefix-replacement" ${VKENUMCLASSES_VALUE_PREFIX_REPLACEMENT})
	list(APPEND GENERATOR_ARGLIST "--value-number-prefix" ${VKENUMCLASSES_VALUE_NUMBER_PREFIX})
	if(${VKENUMCLASSES_VALUE_REMOVE_UNDERSCORES})
		list(APPEND GENERATOR_ARGLIST "--remove-underscores")
	endif()
	if(${VKENUMCLASSES_VALUE_REMOVE_STRUCTURE_NAMES})
		list(APPEND GENERATOR_ARGLIST "--remove-structure-names")
	endif()
	if(${VKENUMCLASSES_VALUE_TOLOWER})
		list(APPEND GENERATOR_ARGLIST "--tolower")
	endif()
	if(${VKENUMCLASSES_VALUE_CAPITALIZE_START})
		list(APPEND GENERATOR_ARGLIST "--capitalize-start")
	endif()
	if(${VKENUMCLASSES_VALUE_REMOVE_POSTFIX})
		list(APPEND GENERATOR_ARGLIST "--value-remove-postfix")
	endif()
	if(${VKENUMCLASSES_VALUE_REMOVE_POSTFIX_NONPOSTFIX_TYPE})
		list(APPEND GENERATOR_ARGLIST "--value-remove-postfix-core-types")
	endif()
endif()
file(MAKE_DIRECTORY "include")
add_custom_command(OUTPUT "include/VulkanEnums.hpp" COMMAND generate
ARGS ${GENERATOR_ARGLIST} WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/include"
DEPENDS generate "$ENV{VULKAN_SDK}/share/vulkan/registry/vk.xml" VERBATIM USES_TERMINAL)

add_library(VulkanEnumClasses INTERFACE "include/VulkanEnums.hpp")
target_include_directories(VulkanEnumClasses INTERFACE "${CMAKE_CURRENT_SOURCE_DIR}/include")
set_target_properties(VulkanEnumClasses PROPERTIES PUBLIC_HEADER "include/VulkanEnums.hpp")

include(GNUInstallDirs)
install(TARGETS VulkanEnumClasses PUBLIC_HEADER)
set(tinyxml2_BUILD_TESTING FALSE CACHE BOOL "Toggles building tests for tinyxml." FORCE)